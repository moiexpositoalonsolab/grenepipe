# =================================================================================================
#     Common
# =================================================================================================


# We first need to load the common functionality, which gives us access to the config file,
# generates the sample list, and prepares some other things for us that are needed below.
include: "rules/initialize.smk"


# =================================================================================================
#     Default "All" Target Rule
# =================================================================================================


# The rule that is executed by default. We include the result files of different intermediate steps
# here as well, for example, the genotyped vcf file from the "calling.smk" step, so that a nice
# arrow shows up in the DAG that reminds us that this is an important intermediate file.
rule all:
    input:
        # Basic steps
        "mapping/final.done",
        "calling/genotyped-all.vcf.gz",
        "calling/filtered-all.vcf.gz" if not config["settings"]["filter-variants"] == "none" else [],
        "annotation/snpeff.vcf.gz" if config["settings"]["snpeff"] else [],
        "annotation/vep.vcf.gz" if config["settings"]["vep"] else [],
        # Quality control
        "qc/multiqc.html",
        # Reference genome statistics
        config["data"]["reference-genome"] + ".seqkit",
        # Pileup
        "mpileup/all-pileups.done" if len(config["settings"]["pileups"]) else [],
        # HAFpipe
        "hafpipe/all.csv"
        + (".gz" if config["params"]["hafpipe"].get("compress-merged-table", False) else "")
        if config["settings"]["hafpipe"]
        else [],
        # Stats. Some deactivated for now.
        "tables/frequencies.tsv" if config["settings"]["frequency-table"] else [],
        # "plots/depths.svg",
        # "plots/allele-freqs.svg",
        # "tables/sample-sizes.tsv"


# The main `all` rule is local. It does not do anything anyway,
# except requesting the other rules to run.
localrules:
    all,


# =================================================================================================
#     Rule Modules
# =================================================================================================

# These rules need to come after the `all` rule above, as Snakemake takes the first rule it finds
# as the implicit target when no other target is specified, and we want that to be the `all` rule.


include: "rules/prepare-reference.smk"
include: "rules/trimming.smk"
include: "rules/mapping.smk"
include: "rules/calling.smk"
include: "rules/filtering.smk"
include: "rules/annotation.smk"
include: "rules/qc.smk"
include: "rules/stats.smk"
include: "rules/damage.smk"
include: "rules/pileup.smk"
include: "rules/frequency.smk"
